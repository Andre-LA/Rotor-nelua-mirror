local math = require 'math'
local os   = require 'os'

local storage   = require 'rotor.storage'
local component = require 'rotor.component'
local entity    = require 'rotor.entity'
local system    = require 'rotor.system'

local ENTITY_COUNT: uinteger <comptime> = #[ E_COUNT or 50000 ]#

-- components
local Position = @component(@record{ x: integer, y: integer })
local Velocity = @component(@record{ x: integer, y: integer })
local Name     = @component(@record{ data: string })

local Rightmost = @record{
  pos: Position,
  name: Name
}

local rightmost: Rightmost

-- systems
local MovementSystem = @record{}

function MovementSystem:run(c: record{vel: *Velocity, pos: *Position})
  c.pos.x = c.pos.x + c.vel.x
  c.pos.y = c.pos.y + c.vel.y
end

local RightmostSystem = @record{
  rightmost_position: integer,
}

function RightmostSystem:init()
  self.rightmost_position = math.mininteger
end

function RightmostSystem:run(c: record{pos: *Position, name: *Name})
  if c.pos.x > self.rightmost_position then
    self.rightmost_position  = c.pos.x
    rightmost.pos = $c.pos
    rightmost.name = $c.name
  end
end


local Systems = @record{
  movement_system: system(MovementSystem.run),
  rightmost_system: system(RightmostSystem.run),
}

-- Entity type
local BasicEntity = @entity(@record{
  position: Position,
  velocity: Velocity,
  name: Name,
})

local entity_storage: storage(BasicEntity, ENTITY_COUNT)
local systems: Systems

systems.rightmost_system.data:init()

-- let's create the entities!
print 'creating!'

for i = 0, < ENTITY_COUNT do
  math.randomseed(os.time())

  -- create an new entity with an position, velocity and name
  entity_storage:push({
    position = { x = math.random(-100, 100), y = i },
    velocity = { x = math.random(-200, 200), y = 0 },
    name = { 'some entity' },
  })
end

 --let's execute movement system 10x
for _ = 1, 10 do
  print ('executing movement system')
  systems.movement_system:run(&entity_storage)
end

print ('executing rightmost system')
systems.rightmost_system:run(&entity_storage)

local name, pos_x = rightmost.name, rightmost.pos.x
print ("entity '", name.data, "' is in the rightmost position x: ", pos_x)
